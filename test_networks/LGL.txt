CTLA4* = TCR
TCR* = Stimuli AND NOT CTLA4
PDGFR* = S1P OR PDGF
FYN* = TCR OR IL2RB
Cytoskeleton_signaling* = FYN
LCK* = CD45 OR (TCR AND NOT ZAP70) OR (IL2RB AND NOT ZAP70)
ZAP70* = LCK AND NOT FYN
GRB2* = IL2RB OR ZAP70
PLCG1* = GRB2 OR PDGFR
RAS* = (GRB2 OR PLCG1) AND NOT GAP
GAP* = (RAS OR PDGFR) AND (RAS OR GAP) AND NOT IL15 AND NOT IL2
#use the above one or the below one
#GAP* = (RAS AND NOT IL15 AND NOT IL2) OR (PDGFR AND GAP AND NOT IL15 AND NOT IL2)
MEK* = RAS
ERK* = MEK AND PI3K
PI3K* = PDGFR OR RAS
NFKB* = (TPL2 OR PI3K) OR (FLIP AND TRADD AND IAP)
NFAT* = PI3K
RANTES* = NFKB
IL2* = (NFKB OR STAT3 OR NFAT) AND NOT TBET
IL2RBT* = ERK AND TBET
IL2RB* = IL2RBT AND (IL2 OR IL15)
IL2RAT* = IL2 AND (STAT3 OR NFKB)
IL2RA* = IL2 AND IL2RAT AND NOT IL2RA
JAK* = (IL2RA OR IL2RB OR RANTES OR IFNG) AND NOT (SOCS OR CD45)
SOCS* = JAK AND NOT (IL2 OR IL15)
STAT3* = JAK
P27* = STAT3
Proliferation* = STAT3 AND NOT P27
TBET* = JAK OR TBET
CREB* = ERK AND IFNG
IFNGT* = TBET OR STAT3 OR NFAT
IFNG* = (IL2 OR IL15 OR Stimuli) AND IFNGT AND NOT (SMAD OR P2)
#IFNG* = ((IL2 OR IL15 OR Stimuli) AND IFNGT) AND NOT (SMAD OR P2)
P2* = (IFNG OR P2) AND NOT Stimuli2
GZMB* = (CREB AND IFNG) OR TBET
TPL2* = TAX OR (PI3K AND TNF)
TNF* = NFKB
TRADD* = TNF AND NOT (IAP OR A20)
FasL* = STAT3 OR NFKB OR NFAT OR ERK
FasT* = NFKB
Fas* = FasT AND FasL AND NOT sFas
sFas* = FasT AND S1P AND NOT Apoptosis
Ceramide* = Fas AND NOT S1P
DISC* = (FasT AND Fas AND IL2) OR (FasT AND Ceramide) OR (FasT AND Fas AND NOT FLIP)
Caspase* = (TRADD AND BID AND NOT IAP) OR (GZMB AND BID AND NOT IAP) OR DISC
FLIP* = (NFKB OR CREB) AND (NFKB OR IFNG) AND NOT DISC
A20* = NFKB
BID* = (Caspase OR GZMB) AND NOT (BclxL OR MCL1)
IAP* = NFKB AND NOT BID
BclxL* = (NFKB OR STAT3) AND NOT (BID OR GZMB OR DISC)
MCL1* = IL2RB AND STAT3 AND NFKB AND PI3K AND NOT DISC
Apoptosis* = Caspase
GPCR* = S1P
SMAD* = GPCR
SPHK1* = PDGFR
S1P* = SPHK1 AND NOT Ceramide